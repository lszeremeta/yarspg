/*
 YARS-PG grammar in Extended Backus-Naur Form (EBNF) notation
 (preview version)
 Based on YARS-PG grammar in ANTLR4
 
 See more at:
 https://github.com/lszeremeta/antlr-yarspg
 
 [The "BSD licence"]
 Copyright (c) 2018-2019, ≈Åukasz Szeremeta (@ University of Bialystok, http://www.uwb.edu.pl/)
 Copyright (c) 2018-2019, Dominik Tomaszuk (@ University of Bialystok, http://www.uwb.edu.pl/)
 All rights reserved.

 Special thanks to Gregg Kellogg (greggkellogg.net) for valuable
 comments to our EBNF (#1 GitHub issue).

 Based on YARS grammar
 (https://github.com/lszeremeta/antlr-yars/blob/master/yars/YARS.g4)
 distributed under BSD licence.

 Redistribution and use in source and binary forms, with or without
 modification, are permitted provided that the following conditions
 are met:
 1. Redistributions of source code must retain the above copyright
    notice, this list of conditions and the following disclaimer.
 2. Redistributions in binary form must reproduce the above copyright
    notice, this list of conditions and the following disclaimer in the
    documentation and/or other materials provided with the distribution.
 3. The name of the author may not be used to endorse or promote products
    derived from this software without specific prior written permission.

 THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
*/

yarspg   ::= statement*
statement
         ::= node
           | edge
           | prefix_directive
           | metadata
           | node_schema
           | edge_schema
           | section
prefix_directive
         ::= pname IRI
pname    ::= ':' ALNUM_PLUS ':'
pn_local ::= ALNUM_PLUS
metadata ::= '-' ((pn_local pname) | (IRI ':')) (STRING | IRI)
graph_name
         ::= STRING
annotation
         ::= string_annotation
           | rdf_annotation
string_annotation
         ::= key ':' STRING
rdf_annotation
         ::= ((pn_local pname) | (IRI ':')) (STRING | IRI)
annotations_list
         ::= '+' annotation (',' annotation)*
props_list
         ::= '[' prop (',' prop)* ']'
meta_prop
         ::= '@' key ':' value
graphs_list
         ::= '/' graph_name (',' graph_name)* '/'
node
         ::= '<' node_id '>' ('{' node_label (',' node_label)* '}')? props_list? graphs_list? annotations_list?
edge
         ::= directed
           | undirected
section  ::= '%' SECTION_NAME           
directed ::= '(' node_id ')' '-' ('<' edge_id '>')? '{' edge_label '}' props_list? '->' '(' node_id ')' graphs_list? annotations_list?
undirected
         ::= '(' node_id ')' '-' ('<' edge_id '>')? '{' edge_label '}' props_list? '-' '(' node_id ')' graphs_list? annotations_list?
node_id  ::= STRING
node_label
         ::= STRING
prop     ::= key ':' value meta_prop*       
edge_id   ::= STRING
edge_label
         ::= STRING
key      ::= STRING
value    ::= primitive_value
           | complex_value
primitive_value
         ::= STRING
           | DATETYPE
           | NUMBER
           | BOOL
           | 'null'
complex_value
         ::= set
           | list
           | struct
set      ::= '{' ( primitive_value | set ) meta_prop* ( ',' ( primitive_value | set ) meta_prop* )* '}'
list     ::= '[' ( primitive_value | list ) meta_prop* ( ',' ( primitive_value | list ) meta_prop* )* ']'
struct   ::= '{' key ':' ( primitive_value | struct ) meta_prop* ( ',' key ':' ( primitive_value | struct ) meta_prop* )* '}'
node_schema
         ::= 'S' ('{' node_label (',' node_label)* '}')? props_list_schema? graphs_list? annotations_list?
props_list_schema
         ::= '[' prop_schema (',' prop_schema)* ']'
prop_schema
         ::= key ':' value_schema id_key?
meta_prop_schema
         ::= '@' key ':' value_schema
value_schema
         ::= primitive_value_schema ( 'ID' | 'NULL' )? meta_prop_schema*
           | complex_value_schema 'NULL'?
primitive_value_schema
         ::= 'Decimal'
           | 'SmallInt'
           | 'Integer'
           | 'BigInt'
           | 'Float'
           | 'Real'
           | 'Double'
           | 'Bool'
           | 'String'
           | 'Date'
           | 'DateTime'
           | 'Time'
complex_value_schema
         ::= set_schema
           | list_schema
           | struct_schema
set_schema
         ::= 'Set' '(' ( primitive_value_schema 'NULL'? | set_schema ) ')' meta_prop_schema*
list_schema
         ::= 'List' '(' ( primitive_value_schema 'NULL'? | list_schema ) ')' meta_prop_schema*
struct_schema
         ::= 'Struct' '(' ( primitive_value_schema 'NULL'? | struct_schema ) ')' meta_prop_schema*
edge_schema
         ::= directed_schema
           | undirected_schema
directed_schema
         ::= 'S' ('(' node_label ')')? '-' '{' edge_label '}' props_list_schema? '->' ('(' node_label ')')?
undirected_schema
         ::= 'S' ('(' node_label ')')? '-' '{' edge_label '}' props_list_schema? '-' ('(' node_label ')')?

SECTION_NAME
         ::= 'METADATA'
           | 'NODE SCHEMAS'
           | 'EDGE SCHEMAS'
           | 'NODES'
           | 'EDGES'
COMMENT  ::= '#' [^#xd#xa#xc]*
STRING   ::= STRING_LITERAL_QUOTE
NUMBER   ::= SIGN? [0-9]+ '.'? [0-9]*
BOOL     ::= 'true'
           | 'false'
DATETYPE ::= DATETIME
           | DATE
           | TIME
DATE     ::= [0-9] [0-9] [0-9] [0-9] '-' [0-9] [0-9] '-' [0-9] [0-9]
TIME     ::= [0-9] [0-9] ':' [0-9] [0-9] ':' [0-9] [0-9] TIMEZONE?
TIMEZONE ::= SIGN? [0-9] [0-9] ':' [0-9] [0-9]
DATETIME
         ::= DATE 'T' TIME
SIGN     ::= '+'
           | '-'
STRING_LITERAL_QUOTE
         ::= '"' ([^"\#xd#xa] | "'" | '\"')* '"'
ALNUM_PLUS
         ::= PN_CHARS_BASE ((PN_CHARS | '.')* PN_CHARS)?
IRI  ::= '<' (PN_CHARS | '.' | ':' | '/' | '\' | '#' | '@' | '%' | '&' | UCHAR)* '>'
PN_CHARS ::= PN_CHARS_U
           | [-0-9#xB7#x0300-#x036F#x203F-#x2040]
PN_CHARS_U
         ::= PN_CHARS_BASE
           | '_'
UCHAR    ::= ('\u' | '\U' HEX HEX HEX HEX) HEX HEX HEX HEX
PN_CHARS_BASE
         ::= [A-Za-z0-9#xC0-#xD6#xD8-#xF6#xF8-#x2FF#x370-#x37D#x37F-#x1FFF#x200C-#x200D#x2070-#x218F#x2C00-#x2FEF#x3001-#xD7FF#xF900-#xFDCF#xFDF0-#xFFFD]
HEX      ::= [0-9A-Fa-f]
WS       ::= ([#x20#x9#xa])+